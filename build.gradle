// Gradle build script for the Acorus project

plugins {
    id 'base' // to add a "clean" task to the root project
}

ext {
    jmeTarget = '' // distinguish non-JME libraries built for specific JME releases

    // current version of the JMonkeyEngine libraries:
    jme3Version = '3.6.1-stable'

    // module coordinates of external dependencies:
    heartCoordinates = 'com.github.stephengold:Heart:9.0.0' + jmeTarget

    //acorusSnapshot = '-SNAPSHOT' // for development builds
    acorusSnapshot = '' // for release builds
    acorusVersion = '2.0.0' + jmeTarget
}

subprojects {
    apply from: rootProject.file('common.gradle')
}

configurations.configureEach {
    resolutionStrategy.cacheChangingModulesFor 0, 'seconds' // to disable caching of snapshots
}

tasks.register('AppChooser') {
    dependsOn ':AcorusExamples:AppChooser'
    description 'Runs the example apps.'
}

tasks.register('checkstyle') {
    dependsOn ':AcorusExamples:checkstyleMain', ':AcorusLibrary:checkstyleMain'
    description 'Checks the style of all Java sourcecode.'
}

// Register cleanup tasks:

clean.dependsOn('cleanNodeModules')
tasks.register('cleanNodeModules', Delete) {
    delete 'node_modules'
}

// Register publishing tasks:

tasks.register('install') {
    dependsOn ':AcorusLibrary:install'
    description 'Installs Maven artifacts to the local repository.'
}
tasks.register('release') {
    dependsOn ':AcorusLibrary:release'
    description 'Stages Maven artifacts to Sonatype OSSRH.'
}
